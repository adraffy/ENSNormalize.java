plugins {
	id 'java-library'
	id 'maven-publish'
	id 'signing'
}

description = 'ENSIP 15'
group = 'io.github.adraffy'
version = '0.3.1'

repositories {
	mavenCentral()
}

dependencies {
	testImplementation platform("org.junit:junit-bom:5.11.0")
	testImplementation "org.junit.jupiter:junit-jupiter"
	testRuntimeOnly "org.junit.platform:junit-platform-launcher"
	testImplementation 'org.json:json:20230618'
}

java {
	//sourceCompatibility = JavaLanguageVersion.of(8)
	//targetCompatibility = JavaLanguageVersion.of(8)
	toolchain {
		languageVersion = JavaLanguageVersion.of(8)
	}
	withSourcesJar()
	withJavadocJar()
}

test {
	useJUnitPlatform()
}

javadoc {
    options.encoding = 'UTF-8'
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
	options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			//groupId = project.group
			artifactId = 'ens-normalize'
			//version = project.version
			from components.java
			pom {
				name = 'ENSNormalize'
				description = project.description
				url = 'https://github.com/adraffy/ENSNormalize.java'
				licenses {
					license {
						name = 'MIT License'
						url = 'https://github.com/adraffy/ENSNormalize.java/blob/main/LICENSE'
					}
				}
				developers {
					developer {
						id = 'raffy'
						name = 'raffy.eth'
						email = 'raffy@me.com'
					}
				}
				scm {
					connection = 'scm:git:https://github.com/adraffy/ENSNormalize.java.git'
					developerConnection = 'scm:git:https://github.com/adraffy/ENSNormalize.java.git'
					url = 'https://github.com/adraffy/ENSNormalize.java'
				}
			}
		}
	}
	repositories {
		maven {
			name = 'ossrh-staging-api'
			url = "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/"
  			credentials {
				username = project.properties.accessToken
				password = project.properties.accessTokenPassword
			}
		}
	}
}

signing {
	sign publishing.publications.mavenJava
        sign jar
}
